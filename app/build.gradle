plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    // FIXME there's no way possible for using plugin w/o version on version catalogs (groovy)
    //  https://github.com/gradle/gradle/issues/17968
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
}

apply from: "$rootDir/config-android.gradle"
apply from: "$rootDir/config-compose.gradle"
apply from: "$rootDir/config-properties.gradle"

android {
    namespace 'com.ujizin.leafy'
    defaultConfig {
        applicationId "com.ujizin.leafy"
        versionCode 1
        versionName "0.1.0"
    }

    signingConfigs {
        release {
            storeFile file(rootProject.ext["RELEASE_STORE_FILE"])
            storePassword rootProject.ext["RELEASE_STORE_PASSWORD"]
            keyAlias rootProject.ext["RELEASE_KEY_ALIAS"]
            keyPassword rootProject.ext["RELEASE_KEY_PASSWORD"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            resValue "string", "app_name", "Leafy"
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue "string", "app_name", "Leafy Dev"
        }
    }
}

dependencies {
    implementation libs.androidx.core.splashscreen

    implementation projects.features.home
    implementation projects.features.search
    implementation projects.features.alarm
    implementation projects.features.camera
    implementation projects.features.publish
    implementation projects.features.about
    implementation projects.features.preferences
    implementation projects.features.tasks
    implementation projects.features.plant

    implementation projects.domain
    implementation projects.core.local

    implementation projects.core.ui
    implementation projects.core.themes
    implementation projects.core.navigation
}
